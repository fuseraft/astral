##
Search for all Kiwi scripts in all subdirectories in the current directory, excluding all test.kiwi files and kiwilib.
Import each script and record the duration of each run.

Iterate through all the recorded runtimes per script and print the details of each, then print the totals at the end.

I really hope you enjoy Kiwi as much as I do! :)
##

import "@kiwi/fileio"
import "@kiwi/time"

@runtimes = []

println "Searching for all Kiwi scripts in all subdirectories in the current directory."
for @file in fileio::glob("./**/*.kiwi") do
  if !(@file.contains("test.kiwi") || @file.contains("/kiwilib/"))
    println "Importing: ${@file}"

    @runtime = {"file": @file}

    # Import the script and record the duration.
    @start = time::ticks()
    import @file
    @stop = time::ticks()

    @runtime["duration"] = @stop - @start
    @runtimes << @runtime
  end
end

@totalRuntime = 0

for @runtime, @testno in @runtimes do
  @ticks = @runtime["duration"]
  @file = @runtime["file"]
  @duration = time::ticksms(@ticks)
  @totalRuntime += @duration

  println "Test #${@testno}, ran ${@file} for ${@duration}ms."
end

println "Ran ${@runtimes.size()} test(s) in ${@totalRuntime}ms."
